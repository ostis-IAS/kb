agent_of_finding_artwork_by_artist_and_year 
=> nrel_main_idtf:
	[агентная scp-программа поиска произведения изобразительного искусства по художнику и году написания] (* <- lang_ru;; *);
	[agent scp-program of finding artwork with artist and year of painting] (* <- lang_en;; *);
	<- agent_scp_program;;

scp_program -> agent_of_finding_artwork_by_artist_and_year
	(*
	-> rrel_params: .agent_of_finding_artwork_params 
		(*
		-> rrel_1: rrel_in: _event;;
		-> rrel_2: rrel_in: _input_arc;;
		*);;

	-> rrel_operators: .agent_of_finding_artwork_operator_set 
		(*
		-> rrel_init: .agent_of_finding_artwork_operator1A 
			(*
		  	<- searchElStr3;;
		  	-> rrel_1: rrel_assign: rrel_scp_var: _temp;;
		  	-> rrel_2: rrel_fixed: rrel_scp_var: _input_arc;;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _quest;;

		  	=> nrel_goto: .agent_of_finding_artwork_operator1B;;
			*);;

		-> .agent_of_finding_artwork_operator1B 
			(*
		  	<- searchElStr3;;
		  	-> rrel_1: rrel_fixed: rrel_scp_const: action_of_finding_artwork_by_artist_and_year;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		  	-> rrel_3: rrel_fixed: rrel_scp_var: _quest;;

		  	=> nrel_then: .agent_of_finding_artwork_operator1C;;
		  	=> nrel_else: .agent_of_finding_artwork_operator_return;;
			*);;

		-> .agent_of_finding_artwork_operator1C
			(*
		 	<- searchElStr5;;

		 	-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_assign: rrel_scp_var: _param1;;
            -> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		 	-> rrel_5: rrel_fixed: rrel_scp_const: rrel_1;;

			=> nrel_then: .agent_of_finding_artwork_operator1D;;
			=> nrel_else: .agent_of_finding_artwork_operator_return;;
			*);;

		-> .agent_of_finding_artwork_operator1D
			(*
		 	<- searchElStr5;;

		 	-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_assign: rrel_scp_var: _param2;;
            -> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		 	-> rrel_5: rrel_fixed: rrel_scp_const: rrel_2;;

			=> nrel_then: .agent_of_finding_artwork_operator2A;;
			=> nrel_else: .agent_of_finding_artwork_operator_return;;
			*);;	 	

		-> .agent_of_finding_artwork_operator2A 
			(*
			<- genEl;;

			-> rrel_1: rrel_assign: rrel_const: rrel_node: rrel_scp_var: _answer;;

			=> nrel_goto: .agent_of_finding_artwork_operator2B;;
			*);;

		-> .agent_of_finding_artwork_operator2B
			(*
			<- sys_search;;

			-> rrel_1: rrel_fixed: rrel_scp_const: [* _pic _<- concept_picture;;
								  _pic _=> nrel_author:: _author;;
								  _author _<- concept_artist;; 
								  _pic _=> nrel_date_of_painting:: _dateP;;
								  _dateP _-> rrel_number_of_year:: _year;; *];;
			-> rrel_2: rrel_fixed: rrel_scp_const: ...;;
			-> rrel_3: rrel_fixed: rrel_scp_const: ... (* -> ... (* -> rrel_1: rrel_fixed: rrel_scp_const: _author;;
										-> rrel_2: rrel_fixed: rrel_scp_var: _param1;; *);;
								      -> ... (* -> rrel_1: rrel_fixed: rrel_scp_const: _year;;
										-> rrel_2: rrel_fixed: rrel_scp_var: _param2;; *);;
								   *);;
			-> rrel_4: rrel_fixed: rrel_scp_var: _answer;;

			=> nrel_then: .agent_of_finding_artwork_operator2BC;;
			=> nrel_else: .agent_of_finding_artwork_operator_return;;			
			*);;

		-> .agent_of_finding_artwork_operator2BC 
			(*
			<- printEl;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;

			=> nrel_goto: .agent_of_finding_artwork_operator2C;;
			*);;  

		-> .agent_of_finding_artwork_operator2C
			(*
			<- searchSetStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: rrel_const: _node;;

			-> rrel_set_3: rrel_fixed: rrel_scp_var: _answer;;

			=> nrel_goto: .agent_of_finding_artwork_operator3A;; 
			*);;
			
		-> .agent_of_finding_artwork_operator3A
			(*
			<- genElStr3;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		  	-> rrel_3: rrel_fixed: rrel_scp_var: _param1;;

		  	=> nrel_goto: .agent_of_finding_artwork_operator3B;;
			*);;
			
		-> .agent_of_finding_artwork_operator3B
			(*
			<- genElStr3;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		  	-> rrel_3: rrel_fixed: rrel_scp_var: _param2;;

		  	=> nrel_goto: .agent_of_finding_artwork_operator3C;;
			*);;

		-> .agent_of_finding_artwork_operator3C
			(*
			<- genElStr3;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		  	-> rrel_3: rrel_fixed: rrel_scp_const: concept_picture;;

		  	=> nrel_goto: .agent_of_finding_artwork_operator3D;;
			*);;
			
		-> .agent_of_finding_artwork_operator3D
			(*
			<- genElStr3;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		  	-> rrel_3: rrel_fixed: rrel_scp_const: nrel_author;;

		  	=> nrel_goto: .agent_of_finding_artwork_operator3E;;
			*);;

		-> .agent_of_finding_artwork_operator3E
			(*
			<- genElStr3;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		  	-> rrel_3: rrel_fixed: rrel_scp_const: concept_artist;;

		  	=> nrel_goto: .agent_of_finding_artwork_operator3F;;
			*);;
			
		-> .agent_of_finding_artwork_operator3F
			(*
			<- genElStr3;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		  	-> rrel_3: rrel_fixed: rrel_scp_const: nrel_date_of_painting;;

		  	=> nrel_goto: .agent_of_finding_artwork_operator3G;;
			*);;

		-> .agent_of_finding_artwork_operator3G
			(*
			<- genElStr3;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		  	-> rrel_3: rrel_fixed: rrel_scp_const: rrel_number_of_year;;

		  	=> nrel_goto: .agent_of_finding_artwork_operator3H;;
			*);;

		-> .agent_of_finding_artwork_operator3H
			(*
			<- genElStr5;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
		  	-> rrel_2: rrel_assign: rrel_const: rrel_common: rrel_scp_var: _arc;;
		  	-> rrel_3: rrel_fixed: rrel_scp_var: _answer;;
		  	-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		  	-> rrel_5: rrel_fixed: rrel_scp_const: nrel_answer;;

		  	=> nrel_goto: .agent_of_finding_artwork_operator_return;;
			*);;

		-> .agent_of_finding_artwork_operator_return 
			(*
		  	<- return;;
			*);;
		*);;
	*);;
